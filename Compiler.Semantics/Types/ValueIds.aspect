◊namespace Azoth.Tools.Bootstrap.Compiler.Semantics.Types;
◊name ValueIdsAspect;
◊using Azoth.Tools.Bootstrap.Compiler.Types.Flow;

// ↓ ⟳ ↑ ⮡ ↗↖ →*← ✎

// Type Declarations
struct `ValueId`;

// Necessary for "stable" because otherwise new ValueIds will be generated repeatedly beneath rewrites
↓ stable *.ValueIdScope <: `ValueIdScope`;

// ---------- Special Parts
↑ ElseClause.ValueId: `ValueId`;

// ---------- Bindings
↑ Binding.BindingValueId: `ValueId`;

// ---------- Definitions
↑ eager ExecutableDefinition.ValueIdScope: `ValueIdScope`;
= ExecutableDefinition.ValueIdScope;
= ExecutableDefinition.*.**.ValueIdScope => ValueIdScope;

// ---------- Type Definitions
/// While type definitions don't truly have a `ValueIdScope`, one is needed for the type names
↑ eager TypeDefinition.ValueIdScope: `ValueIdScope`;
= TypeDefinition.ValueIdScope;
= TypeDefinition.Attributes[*].**.ValueIdScope => ValueIdScope;
= TypeDefinition.SupertypeNames[*].**.ValueIdScope => ValueIdScope;
= ClassDefinition.BaseTypeName.**.ValueIdScope => ValueIdScope;
// Do not inherit the value ValueIdScope to members to avoid a situation where the scope is
// accidentally used when it shouldn't be.

// ---------- Parameters
// TODO inheriting this doesn't need to cause cycle with rewritten parent
↓ computed Parameter.ValueIdScope: `ValueIdScope`;
↑ Parameter.BindingValueId: `ValueId`;
= Parameter.BindingValueId;

// ---------- Statements
↑ Statement.ResultValueId: `ValueId`?;
= ResultStatement.ResultValueId => ValueId;
= ResultStatement.ValueId => Expression?.ValueId ?? default;
// TODO inheriting this doesn't need to cause cycle with rewritten parent
↓ computed VariableDeclarationStatement.ValueIdScope: `ValueIdScope`;
= VariableDeclarationStatement.ResultValueId => null;
= ExpressionStatement.ResultValueId => null;

// ---------- Patterns
↓ Pattern.MatchReferentValueId: `ValueId`?;
// TODO inheriting this doesn't need to cause cycle with rewritten parent
↓ computed BindingPattern.ValueIdScope: `ValueIdScope`;

// ---------- Expressions
// TODO inheriting this doesn't need to cause cycle with rewritten parent
↓ computed Expression.ValueIdScope: `ValueIdScope`;
↑ Expression.ValueId: `ValueId`;
= Expression.ValueId;

// ---------- Operator Expressions
= PatternMatchExpression.Pattern.**.MatchReferentValueId => Referent?.ValueId;
