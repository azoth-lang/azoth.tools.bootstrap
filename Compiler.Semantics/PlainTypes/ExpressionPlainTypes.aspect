◊namespace Azoth.Tools.Bootstrap.Compiler.Semantics.PlainTypes;
◊name ExpressionPlainTypesAspect;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Diagnostics;
◊using Azoth.Tools.Bootstrap.Compiler.Types.Plain;
◊using AzothPlainType = Azoth.Tools.Bootstrap.Compiler.Types.Plain.PlainType;

// ↓ ⟳ ↑ ⮡ ↗↖ →*← ✎

// TODO ExpectedPlainType could really be INonVoidPlainType?, but regular types need to be made more strict first.

// ---------- Special Parts
↑ BlockOrResult.PlainType: `IMaybePlainType`;

// ---------- Function Parts
↓ ExpressionBody.ExpectedPlainType: `IMaybePlainType`?;
= ExpressionBody.ResultStatement.ExpectedPlainType;

// ---------- Statements
↑ Statement.ResultPlainType: `IMaybePlainType`? => null;
↓ ResultStatement.ExpectedPlainType: `IMaybePlainType`?;
= ResultStatement.Expression.ExpectedPlainType => ExpectedPlainType;
= ResultStatement.ResultPlainType => PlainType;
// The expected plain type for the initializer is the binding plain type since it needs to
// have any conversion (e.g. to non-const-value type) applied.
= VariableDeclarationStatement.Initializer.ExpectedPlainType => BindingPlainType;
= ExpressionStatement.Expression.ExpectedPlainType => null;

// ---------- Patterns
= OptionalPattern.↑.Diagnostics;

// ---------- Expressions
// TODO remove once all nodes properly provide the expected plain type
= AmbiguousExpression.*.ExpectedPlainType => null;
↑ Expression.PlainType: `IMaybePlainType`;
↓ OrdinaryTypedExpression.ExpectedPlainType: `IMaybePlainType`?;
✎ OrdinaryTypedExpression insert ImplicitConversionExpression;
✎ OrdinaryTypedExpression insert ImplicitDerefExpression;
✎ OrdinaryTypedExpression OptionalConversion rewrite OptionalConversionExpression;
↑ BlockExpression.PlainType: `IMaybePlainType`;
= NeverTypedExpression.PlainType => AzothPlainType.Never;

// ---------- Unresolved Expressions
= UnresolvedMemberAccessExpression.PlainType => AzothPlainType.Unknown;
= UnresolvedMemberAccessExpression.*.ExpectedPlainType => null;

// ---------- Instance Member Access Expressions
= MethodAccessExpression.Context.ExpectedPlainType;

// ---------- Operator Expressions
= AssignmentExpression.RightOperand.ExpectedPlainType => LeftOperand?.PlainType;
✎ AssignmentExpression replace_with RefAssignmentExpression;
= RefAssignmentExpression.LeftOperand.ExpectedPlainType
    => RefPlainType.Create(isInternal: false, isMutableBinding: true, RightOperand?.PlainType);
= RefAssignmentExpression.RightOperand.ExpectedPlainType
    => (LeftOperand?.PlainType as RefPlainType)?.Referent;
↑ BinaryOperatorExpression.NumericOperatorCommonPlainType: `PlainType`?;
= BinaryOperatorExpression.LeftOperand.ExpectedPlainType => NumericOperatorCommonPlainType;
= BinaryOperatorExpression.RightOperand.ExpectedPlainType => NumericOperatorCommonPlainType;
= UnaryOperatorExpression.↑.Diagnostics;
// No expected plain type for the referent. If one were given, it could cause another implicit conversion.
= ImplicitConversionExpression.Referent.ExpectedPlainType => null;
= OptionalConversionExpression.Referent.ExpectedPlainType => null;
= PatternMatchExpression.PlainType => AzothPlainType.Bool;
= RefExpression.Referent.ExpectedPlainType => null;
= ImplicitDerefExpression.Referent.ExpectedPlainType => null;

// ---------- Control Flow Expressions
// To avoid implicit conversion to `bool?` the expected type is `bool` when the condition type is `bool`
= IfExpression.Condition.ExpectedPlainType
    => AzothPlainType.Bool.Equals((CurrentCondition as IExpressionNode)?.PlainType)
        ? AzothPlainType.Bool : AzothPlainType.OptionalBool;
= WhileExpression.Condition.ExpectedPlainType
    => AzothPlainType.Bool.Equals((CurrentCondition as IExpressionNode)?.PlainType)
        ? AzothPlainType.Bool : AzothPlainType.OptionalBool;
↓ ReturnExpression.ExpectedReturnPlainType: `IMaybePlainType`?;
= ReturnExpression.Value.ExpectedPlainType => ExpectedReturnPlainType;

// ---------- Invocation Expressions
= UnresolvedInvocationExpression.PlainType => AzothPlainType.Unknown;
= FunctionInvocationExpression.Arguments[index].ExpectedPlainType
    => Self.SelectedCallCandidate?.ParameterPlainTypes[index];
= MethodInvocationExpression.Arguments[index].ExpectedPlainType
    => Self.SelectedCallCandidate?.ParameterPlainTypes[index];
= GetterInvocationExpression.Context.ExpectedPlainType
    => Self.SelectedCallCandidate?.SelfParameterPlainType;
= SetterInvocationExpression.Context.ExpectedPlainType
    => Self.SelectedCallCandidate?.SelfParameterPlainType;
= SetterInvocationExpression.Arguments[index].ExpectedPlainType
    => Self.SelectedCallCandidate?.ParameterPlainTypes[index];
= FunctionReferenceInvocationExpression.Arguments[index].ExpectedPlainType
    => Self.FunctionPlainType.Parameters[index];
↑ FunctionReferenceInvocationExpression.FunctionPlainType: `FunctionPlainType`;
= InitializerInvocationExpression.Arguments[index].ExpectedPlainType
    => Self.SelectedCallCandidate?.ParameterPlainTypes[index];
= NonInvocableInvocationExpression.PlainType => AzothPlainType.Unknown;

// ---------- Name Expressions
= FunctionNameExpression.*.ExpectedPlainType => null;
↑ InitializerNameExpression.InitializingPlainType: `IMaybePlainType` => Context.NamedPlainType;
= MissingNameExpression.PlainType => AzothPlainType.Unknown;

// ---------- Unresolved Name Expressions
= UnresolvedNameExpression.PlainType => AzothPlainType.Unknown;
= UnresolvedQualifiedNameExpression.PlainType => AzothPlainType.Unknown;

// ---------- Names
= Name.PlainType => AzothPlainType.Unknown;
= NamespaceName.PlainType => AzothPlainType.Unknown;

// ---------- Unresolved Names
= UnresolvedName.PlainType => AzothPlainType.Unknown;

// ---------- Capability Expressions
= MoveExpression.PlainType => Referent.PlainType;
= ImplicitTempMoveExpression.PlainType => Referent.PlainType;
= FreezeExpression.PlainType => Referent.PlainType;
= PrepareToReturnExpression.Value.ExpectedPlainType => ExpectedPlainType;
= PrepareToReturnExpression.PlainType => Value.PlainType;

// ---------- Async Expressions
= AsyncBlockExpression.PlainType => Block.PlainType;
= AwaitExpression.↑.Diagnostics;
