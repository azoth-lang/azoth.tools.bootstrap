◊namespace Azoth.Tools.Bootstrap.Compiler.Syntax;
◊root-supertype <default>;
◊prefix I;
◊suffix Syntax;
◊class-suffix Syntax;
◊simplified-tree true;
◊using System.Numerics;
◊using Azoth.Tools.Bootstrap.Compiler.Core;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Code;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Diagnostics;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Operators;
◊using Azoth.Tools.Bootstrap.Compiler.Names;
◊using Azoth.Tools.Bootstrap.Compiler.Symbols;
◊using Azoth.Tools.Bootstrap.Compiler.Tokens;
◊using Azoth.Tools.Bootstrap.Compiler.Types.Capabilities;
◊using Azoth.Tools.Bootstrap.Framework;

// TODO:
//     The syntax tree ought to be independent of semantics. It should not try to enforce rules
//     such as that certian kinds of expressions are expected in certian places. It should collapse
//     node types even if they appear in different contexts. For example, a `SpecialTypeName` and a
//     `SpecialTypeNameExpression` should be the same node type.

<default>;

// ---------- Top Level
/// Syntax that corresponds to Azoth code.
Code = Span:`TextSpan`;
CompilationUnit <: Code = File:`CodeFile` ImplicitNamespaceName:`NamespaceName` Diagnostics:`DiagnosticCollection`
    ImportDirectives:ImportDirective* Definitions:NamespaceBlockMemberDefinition*;
ImportDirective <: Code = Name:`NamespaceName`;

// ---------- Special Parts
BodyOrBlock <: Code = Statements:Statement*;
ElseClause <: Code;
BlockOrResult <: ElseClause;

// ---------- Bindings
Binding <: Code = IsMutableBinding:`bool`;
LocalBinding <: Binding = NameSpan:`TextSpan`;

// ---------- Packages
Package = Name:`IdentifierName` CompilationUnits:{CompilationUnit} TestingCompilationUnits:{CompilationUnit}
    References:{PackageReference};
PackageReference = AliasOrName:`IdentifierName` Package:`IPackageSymbols` IsTrusted:`bool`;

// ---------- Definitions
Definition <: Code = File:`CodeFile` Name:`TypeName`? NameSpan:`TextSpan`;
/// All top-level non-namespace definitions since a namespace doesn't really create a thing, it just
/// defines a group of names.
// TODO give a better name to this (the semantic tree has moved away from "entity")
EntityDefinition <: Definition = AccessModifier:`IAccessModifierToken`?;
/// Base type for any declaration that declares a callable thing (i.e. function, method, getter, setter, constructor, or initializer).
InvocableDefinition <: EntityDefinition = Parameters:ConstructorOrInitializerParameter*;
ConcreteInvocableDefinition <: InvocableDefinition = Parameters:ConstructorOrInitializerParameter* Body;

// ---------- Namespace Definitions
NamespaceBlockDefinition <: NamespaceBlockMemberDefinition
    = IsGlobalQualified:`bool` DeclaredNames:`NamespaceName` ImportDirectives:ImportDirective* Definitions:NamespaceBlockMemberDefinition*;
/// Things that can be declared outside of a type (i.e. functions, types, and namespace blocks).
NamespaceBlockMemberDefinition <: Definition;

// ---------- Function Definition
FunctionDefinition <: ConcreteInvocableDefinition, NamespaceBlockMemberDefinition
    = Attributes:Attribute* Name:`IdentifierName` Parameters:NamedParameter* Return? Body;

// ---------- Type Definitions
TypeDefinition <: NamespaceBlockMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, StructMemberDefinition
    = ConstModifier:`IConstKeywordToken`? MoveModifier:`IMoveKeywordToken`? Name:`StandardName` GenericParameters:GenericParameter* SupertypeNames:StandardTypeName* Members:TypeMemberDefinition*;
ClassDefinition <: TypeDefinition
    = AbstractModifier:`IAbstractKeywordToken`? GenericParameters:GenericParameter* BaseTypeName:StandardTypeName? SupertypeNames:StandardTypeName* Members:ClassMemberDefinition*;
StructDefinition <: TypeDefinition
    = GenericParameters:GenericParameter* SupertypeNames:StandardTypeName* Members:StructMemberDefinition*;
TraitDefinition <: TypeDefinition
    = GenericParameters:GenericParameter* SupertypeNames:StandardTypeName* Members:TraitMemberDefinition*;

// ---------- Type Definition Parts
GenericParameter <: Code = Constraint:CapabilityConstraint Name:`IdentifierName` Independence:`TypeParameterIndependence` Variance:`TypeParameterVariance`;

// ---------- Type Member Definitions (i.e. declarations that can be contained in a type)
TypeMemberDefinition <: EntityDefinition;
ClassMemberDefinition <: TypeMemberDefinition;
TraitMemberDefinition <: TypeMemberDefinition;
StructMemberDefinition <: TypeMemberDefinition;

// ---------- Member Definitions
MethodDefinition <: TypeMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, InvocableDefinition
    = Name:`IdentifierName` SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return?;
AbstractMethodDefinition <: MethodDefinition = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return?;
ConcreteMethodDefinition <: MethodDefinition, StructMemberDefinition, ConcreteInvocableDefinition
    = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return? Body;
StandardMethodDefinition <: ConcreteMethodDefinition = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return? Body;
GetterMethodDefinition <: ConcreteMethodDefinition = SelfParameter:MethodSelfParameter Return Body;
SetterMethodDefinition <: ConcreteMethodDefinition = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Body;
ConstructorDefinition <: ConcreteInvocableDefinition, ClassMemberDefinition
    = Name:`IdentifierName`? SelfParameter:ConstructorSelfParameter Parameters:ConstructorOrInitializerParameter* Body:BlockBody;
InitializerDefinition <: ConcreteInvocableDefinition, StructMemberDefinition
    = Name:`IdentifierName`? SelfParameter:InitializerSelfParameter Parameters:ConstructorOrInitializerParameter* Body:BlockBody;
FieldDefinition <: TypeMemberDefinition, ClassMemberDefinition, StructMemberDefinition, Binding
    = Name:`IdentifierName` Type Initializer:Expression?;
AssociatedFunctionDefinition <: TypeMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, StructMemberDefinition, ConcreteInvocableDefinition
    = Name:`IdentifierName` Parameters:NamedParameter* Return? Body;

// ---------- Attributes
Attribute <: Code = TypeName:StandardTypeName;

// ---------- Capabilities
CapabilityConstraint <: Code = Constraint:`ICapabilityConstraint`;
CapabilitySet <: CapabilityConstraint = Constraint:`CapabilitySet`;
Capability <: CapabilityConstraint = Tokens:`ICapabilityToken`* Declared:`DeclaredCapability` Capability:`Capability`;

// ---------- Parameters and Return
Parameter <: Code = Name:`IdentifierName`?;
/// A non-self parameter that can be declared in a constructor
ConstructorOrInitializerParameter <: Parameter;
NamedParameter <: ConstructorOrInitializerParameter, LocalBinding
    = IsMutableBinding:`bool` IsLentBinding:`bool` Name:`IdentifierName` Type DefaultValue:Expression?;
SelfParameter <: Parameter = IsLentBinding:`bool` Capability:CapabilityConstraint;
ConstructorSelfParameter <: SelfParameter = Capability;
InitializerSelfParameter <: SelfParameter = Capability;
MethodSelfParameter <: SelfParameter = Capability:CapabilityConstraint;
FieldParameter <: ConstructorOrInitializerParameter = Name:`IdentifierName` DefaultValue:Expression?;
Return <: Code = Type; // Return syntax of a function or method (i.e. ``)

// ---------- Function Parts
Body <: BodyOrBlock;
BlockBody <: Body = Statements:BodyStatement*;
ExpressionBody <: Body = ResultStatement;

// ---------- Types
Type <: Code;
TypeName <: Type = Name:`TypeName`;
StandardTypeName <: TypeName = Name:`StandardName`;
SimpleTypeName <: TypeName;
IdentifierTypeName <: TypeName, StandardTypeName, SimpleTypeName = Name:`IdentifierName`;
SpecialTypeName <: SimpleTypeName = Name:`SpecialTypeName`;
GenericTypeName <: StandardTypeName = Name:`GenericName` TypeArguments:Type*;
QualifiedTypeName <: TypeName = Context:TypeName QualifiedName:StandardTypeName;
OptionalType <: Type = Referent:Type;
CapabilityType <: Type = Capability Referent:Type;
FunctionType <: Type = Parameters:ParameterType* Return:ReturnType;
ParameterType <: Code = IsLent:`bool` Referent:Type;
ReturnType <: Code = Referent:Type; // Return type of function type
ViewpointType <: Type = Referent:Type;
CapabilityViewpointType <: ViewpointType = Capability Referent:Type;
SelfViewpointType <: ViewpointType = Referent:Type;

// ---------- Statements
Statement <: Code;
ResultStatement <: Statement, BlockOrResult = Expression;

/// A statement that can appear directly in a method body. (i.e. not a result statement)
BodyStatement <: Statement;
VariableDeclarationStatement <: BodyStatement, LocalBinding
    = Name:`IdentifierName` Capability? Type? Initializer:Expression?;
ExpressionStatement <: BodyStatement = Expression;

// ---------- Patterns
Pattern <: Code;
BindingContextPattern <: Pattern = IsMutableBinding:`bool` Pattern Type?;
OptionalOrBindingPattern <: Pattern;
BindingPattern <: OptionalOrBindingPattern, LocalBinding = Name:`IdentifierName`;
OptionalPattern <: OptionalOrBindingPattern = Pattern:OptionalOrBindingPattern;

// ---------- Expressions
Expression <: Code;

/// A block expression. Not to be used to represent function or type bodies.
BlockExpression <: Expression, BlockOrResult, BodyOrBlock = Statements:Statement*;

/// <summary>
/// Note that this could represent a named or unnamed constructor. So
/// for an unnamed constructor, it is really the type name. Conceptually
/// though, the type name is the name of the unnamed constructor. Thus,
/// this expression's type could be either an object type, or member type.
/// </summary>
NewObjectExpression <: Expression
    = Type:TypeName ConstructorName:`IdentifierName`? ConstructorNameSpan:`TextSpan`? Arguments:Expression*;
UnsafeExpression <: Expression = Expression;

// ---------- Literal Expressions
LiteralExpression <: Expression;
BoolLiteralExpression <: LiteralExpression = Value:`bool`;
IntegerLiteralExpression <: LiteralExpression = Value:`BigInteger` ;
NoneLiteralExpression <: LiteralExpression;
StringLiteralExpression <: LiteralExpression = Value:`string`;

// ---------- Operator Expressions
AssignmentExpression <: Expression = LeftOperand:Expression Operator:`AssignmentOperator` RightOperand:Expression;
BinaryOperatorExpression <: Expression = LeftOperand:Expression Operator:`BinaryOperator` RightOperand:Expression;
UnaryOperatorExpression <: Expression = Fixity:`UnaryOperatorFixity` Operator:`UnaryOperator` Operand:Expression;
ConversionExpression <: Expression = Referent:Expression Operator:`ConversionOperator` ConvertToType:Type;
PatternMatchExpression <: Expression = Referent:Expression Pattern;

// ---------- Control Flow Expressions
IfExpression <: Expression, ElseClause = Condition:Expression ThenBlock:BlockOrResult ElseClause?;
LoopExpression <: Expression = Block:BlockExpression;
WhileExpression <: Expression = Condition:Expression Block:BlockExpression;
ForeachExpression <: Expression, LocalBinding
    = VariableName:`IdentifierName` InExpression:Expression Type? Block:BlockExpression;
BreakExpression <: Expression = Value:Expression?;
NextExpression <: Expression;
ReturnExpression <: Expression = Value:Expression?;

// ---------- Invocation Expressions
InvocationExpression <: Expression = Expression Arguments:Expression*;

// ---------- Name Expressions
NameExpression <: Expression;
SimpleName <: NameExpression;
StandardNameExpression <: NameExpression = Name:`StandardName`;
IdentifierNameExpression <: NameExpression, StandardNameExpression, SimpleName
    = Name:`IdentifierName`;
SpecialTypeNameExpression <: NameExpression, Expression = Name:`SpecialTypeName`;
GenericNameExpression <: NameExpression, StandardNameExpression = Name:`GenericName` TypeArguments:Type*;
InstanceExpression <: SimpleName; // i.e. `self` or `base`
SelfExpression <: NameExpression, InstanceExpression = IsImplicit:`bool`;
MemberAccessExpression <: NameExpression
    = Context:Expression MemberName:`StandardName` TypeArguments:Type* MemberNameSpan:`TextSpan`;
MissingName <: NameExpression, SimpleName;

// ---------- Capability Expressions
/// i.e. `move name`. A move takes ownership of something from a variable.
MoveExpression <: Expression = Referent:SimpleName;
FreezeExpression <: Expression = Referent:SimpleName;

// ---------- Async Expressions
AsyncBlockExpression <: Expression = Block:BlockExpression;
AsyncStartExpression <: Expression = Scheduled:`bool` Expression;
AwaitExpression <: Expression = Expression;
