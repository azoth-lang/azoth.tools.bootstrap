◊namespace Azoth.Tools.Bootstrap.Compiler.Syntax;
◊root-supertype <default>;
◊prefix I;
◊suffix Syntax;
◊class-suffix Syntax;
◊simplified-tree true;
◊using System.Numerics;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Code;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Diagnostics;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Operators;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Types;
◊using Azoth.Tools.Bootstrap.Compiler.Names;
◊using Azoth.Tools.Bootstrap.Compiler.Symbols;
◊using Azoth.Tools.Bootstrap.Compiler.Tokens;
◊using Azoth.Tools.Bootstrap.Compiler.Types.Capabilities;
◊using Azoth.Tools.Bootstrap.Framework;

// The syntax tree ought to be independent of semantics. It should not try to enforce rules such as
// that certian kinds of expressions are expected in certian places. It should collapse node types
// even if they appear in different contexts.

<default>;

// ---------- Special Parts
BodyOrBlock <: Code = Statements:Statement*;
ElseClause <: Code;
BlockOrResult <: ElseClause;

// ---------- Bindings
Binding <: Code = IsMutableBinding:`bool`;
LocalBinding <: Binding = NameSpan:`TextSpan`;

// ---------- Packages
Package = Name:`IdentifierName` CompilationUnits:{CompilationUnit} TestingCompilationUnits:{CompilationUnit}
    References:{PackageReference};
PackageReference = AliasOrName:`IdentifierName` Package:`IPackageSymbols` IsTrusted:`bool`;

// ---------- Code Files
/// Syntax that corresponds to Azoth code.
Code = Span:`TextSpan`;
CompilationUnit <: Code = File:`CodeFile` ImplicitNamespaceName:`NamespaceName` Diagnostics:`DiagnosticCollection`
    ImportDirectives:ImportDirective* Definitions:NamespaceBlockMemberDefinition*;
ImportDirective <: Code = Name:`NamespaceName`;

// ---------- Facets
/// All top-level non-namespace definitions since a namespace doesn't really create a thing, it just
/// defines a group of names.
FacetMemberDefinition <: Definition = AccessModifier:`IAccessModifierToken`?;

// ---------- Definitions
Definition <: Code = File:`CodeFile` Name:`UnqualifiedName`? NameSpan:`TextSpan`;
/// Base type for any declaration that declares a callable thing (i.e. function, method, getter, setter, or initializer).
InvocableDefinition <: FacetMemberDefinition = Parameters:InitializerParameter* Body?;

// ---------- Namespace Definitions
NamespaceBlockDefinition <: NamespaceBlockMemberDefinition
    = IsGlobalQualified:`bool` DeclaredNames:`NamespaceName` ImportDirectives:ImportDirective* Definitions:NamespaceBlockMemberDefinition*;
/// Things that can be declared outside of a type (i.e. functions, types, and namespace blocks).
NamespaceBlockMemberDefinition <: Definition;

// ---------- Function Definition
FunctionDefinition <: InvocableDefinition, NamespaceBlockMemberDefinition
    = Attributes:Attribute* Name:`IdentifierName` Parameters:NamedParameter* Return? Body;

// ---------- Type Definitions
TypeDefinition <: NamespaceBlockMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, StructMemberDefinition
    = ConstModifier:`IConstKeywordToken`? MoveModifier:`IMoveKeywordToken`? Name:`OrdinaryName` GenericParameters:GenericParameter* SupertypeNames:Name* Members:TypeMemberDefinition*;
ClassDefinition <: TypeDefinition
    = AbstractModifier:`IAbstractKeywordToken`? GenericParameters:GenericParameter* BaseTypeName:Name? SupertypeNames:Name* Members:ClassMemberDefinition*;
StructDefinition <: TypeDefinition
    = GenericParameters:GenericParameter* SupertypeNames:Name* Members:StructMemberDefinition*;
TraitDefinition <: TypeDefinition
    = GenericParameters:GenericParameter* SupertypeNames:Name* Members:TraitMemberDefinition*;

// ---------- Type Definition Parts
GenericParameter <: Code = Constraint:CapabilityConstraint Name:`IdentifierName` Independence:`TypeParameterIndependence` Variance:`TypeParameterVariance`;

// ---------- Type Member Definitions (i.e. declarations that can be contained in a type)
TypeMemberDefinition <: FacetMemberDefinition;
// TODO eliminate the syntax level restrictions on which members can occur in which definitions
ClassMemberDefinition <: TypeMemberDefinition;
TraitMemberDefinition <: TypeMemberDefinition;
StructMemberDefinition <: TypeMemberDefinition;

// ---------- Member Definitions
MethodDefinition <: TypeMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, StructMemberDefinition, InvocableDefinition
    = Name:`IdentifierName` SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return? Body?;
AbstractMethodDefinition <: MethodDefinition = /SelfParameter/ /Parameters/ Return?;
OrdinaryMethodDefinition <: MethodDefinition = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Return? Body;
GetterMethodDefinition <: MethodDefinition = SelfParameter:MethodSelfParameter Return Body?;
SetterMethodDefinition <: MethodDefinition = SelfParameter:MethodSelfParameter Parameters:NamedParameter* Body;
InitializerDefinition <: InvocableDefinition, StructMemberDefinition, ClassMemberDefinition
    = Name:`IdentifierName`? SelfParameter:InitializerSelfParameter Parameters:InitializerParameter* Body:BlockBody;
FieldDefinition <: TypeMemberDefinition, ClassMemberDefinition, StructMemberDefinition, Binding
    = Name:`IdentifierName` Type Initializer:Expression?;
AssociatedFunctionDefinition <: TypeMemberDefinition, ClassMemberDefinition, TraitMemberDefinition, StructMemberDefinition, InvocableDefinition
    = Name:`IdentifierName` Parameters:NamedParameter* Return? Body;

// ---------- Attributes
Attribute <: Code = TypeName:Name;

// ---------- Capabilities
CapabilityConstraint <: Code;
CapabilitySet <: CapabilityConstraint = CapabilitySet:`CapabilitySet`;
Capability <: CapabilityConstraint = Tokens:`ICapabilityToken`* Capability:`DeclaredCapability`;

// ---------- Parameters and Return
Parameter <: Code = Name:`IdentifierName`?;
/// A non-self parameter that can be declared in an initializer
InitializerParameter <: Parameter;
NamedParameter <: InitializerParameter, LocalBinding
    = IsMutableBinding:`bool` IsLentBinding:`bool` Name:`IdentifierName` Type DefaultValue:Expression?;
SelfParameter <: Parameter = IsLentBinding:`bool` Constraint:CapabilityConstraint;
// TODO support renaming base Constraint property to Capability
InitializerSelfParameter <: SelfParameter = Constraint:Capability;
MethodSelfParameter <: SelfParameter;
FieldParameter <: InitializerParameter = Name:`IdentifierName` DefaultValue:Expression?;
Return <: Code = Type; // Return syntax of a function or method (i.e. ``)

// ---------- Function Parts
Body <: BodyOrBlock;
BlockBody <: Body = Statements:BodyStatement*;
ExpressionBody <: Body = ResultStatement;

// ---------- Types
Type <: Code;
// See the names section for more nodes that are types
OptionalType <: Type = Referent:Type;
CapabilityType <: Type = Capability Referent:Type;
FunctionType <: Type = Parameters:ParameterType* Return:Type;
ParameterType <: Code = IsLent:`bool` Referent:Type;
ViewpointType <: Type = Referent:Type;
CapabilityViewpointType <: ViewpointType = Capability Referent:Type;
SelfViewpointType <: ViewpointType = Referent:Type;

// ---------- Statements
Statement <: Code;
ResultStatement <: Statement, BlockOrResult = Expression;

/// A statement that can appear directly in a method body. (i.e. not a result statement)
BodyStatement <: Statement;
VariableDeclarationStatement <: BodyStatement, LocalBinding
    = Name:`IdentifierName` Capability? Type? Initializer:Expression?;
ExpressionStatement <: BodyStatement = Expression;

// ---------- Patterns
Pattern <: Code;
BindingContextPattern <: Pattern = IsMutableBinding:`bool` Pattern Type?;
OptionalOrBindingPattern <: Pattern;
BindingPattern <: OptionalOrBindingPattern, LocalBinding = Name:`IdentifierName`;
OptionalPattern <: OptionalOrBindingPattern = Pattern:OptionalOrBindingPattern;

// ---------- Expressions
Expression <: Code;

/// A block expression. Not to be used to represent function or type bodies.
BlockExpression <: Expression, BlockOrResult, BodyOrBlock = Statements:Statement*;

UnsafeExpression <: Expression = Expression;

// ---------- Instance Member Access Expressions
/// Note: this has MemberName and GenericArguments instead of a qualified UnqualifiedName name
/// attribute because in the semantic model it becomes confusing to have that. It is too easy for
/// the UnqualifiedName to be treated as a stand-alone name instead of binding it in context.
concrete MemberAccessExpression <: Expression
    = Context:Expression MemberNameSpan:`TextSpan` MemberName:`OrdinaryName` GenericArguments:Type*;

// ---------- Literal Expressions
LiteralExpression <: Expression;
BoolLiteralExpression <: LiteralExpression = Value:`bool`;
IntegerLiteralExpression <: LiteralExpression = Value:`BigInteger` ;
NoneLiteralExpression <: LiteralExpression;
StringLiteralExpression <: LiteralExpression = Value:`string`;

// ---------- Operator Expressions
AssignmentExpression <: Expression = LeftOperand:Expression Operator:`AssignmentOperator` RightOperand:Expression;
BinaryOperatorExpression <: Expression = LeftOperand:Expression Operator:`BinaryOperator` RightOperand:Expression;
UnaryOperatorExpression <: Expression = Fixity:`UnaryOperatorFixity` Operator:`UnaryOperator` Operand:Expression;
ConversionExpression <: Expression = Referent:Expression Operator:`ConversionOperator` ConvertToType:Type;
PatternMatchExpression <: Expression = Referent:Expression Pattern;

// ---------- Control Flow Expressions
IfExpression <: Expression, ElseClause = Condition:Expression ThenBlock:BlockOrResult ElseClause?;
LoopExpression <: Expression = Block:BlockExpression;
WhileExpression <: Expression = Condition:Expression Block:BlockExpression;
ForeachExpression <: Expression, LocalBinding
    = VariableName:`IdentifierName` InExpression:Expression Type? Block:BlockExpression;
BreakExpression <: Expression = Value:Expression?;
NextExpression <: Expression;
ReturnExpression <: Expression = Value:Expression?;

// ---------- Invocation Expressions
InvocationExpression <: Expression = Expression Arguments:Expression*;

// ---------- Name Expressions
// Name expressions are more inclusive than just names since there are names that can occur in an
// expression that don't occur in type names.
NameExpression <: Expression;
// See the names section for more nodes that are name expressions
InstanceExpression <: NameExpression; // i.e. `self` or `base`
SelfExpression <: InstanceExpression = IsImplicit:`bool`;
MissingNameExpression <: NameExpression;

// ---------- Names
// At the syntax level, there is no distinction between names of types and other names, they all use
// the same node types so that rewrites of the semantic tree can refer to them when it is realized a
// name refers to a type. Thus names are both types and name expressions.
Name <: Type, NameExpression;
UnqualifiedName <: Name = Name:`UnqualifiedName`;
BuiltInTypeName <: UnqualifiedName = Name:`BuiltInTypeName`;
OrdinaryName <: UnqualifiedName = Name:`OrdinaryName` GenericArguments:Type*;
IdentifierName <: OrdinaryName = Name:`IdentifierName`;
GenericName <: OrdinaryName = Name:`GenericName` /GenericArguments/;
QualifiedName <: Name, MemberAccessExpression = Context:Name /MemberNameSpan/ /MemberName/ /GenericArguments/;

// ---------- Capability Expressions
/// i.e. `move name`. A move takes ownership of something from a variable or expression.
MoveExpression <: Expression = Referent:Expression;
FreezeExpression <: Expression = Referent:Expression;

// ---------- Async Expressions
AsyncBlockExpression <: Expression = Block:BlockExpression;
AsyncStartExpression <: Expression = Scheduled:`bool` Expression;
AwaitExpression <: Expression = Expression;
