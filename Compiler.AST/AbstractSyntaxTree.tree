◊namespace Azoth.Tools.Bootstrap.Compiler.AST;
◊base 'IAbstractSyntax';
◊prefix I;
◊list IFixedList;
◊using System.Numerics;
◊using Azoth.Tools.Bootstrap.Compiler.Core;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Operators;
◊using Azoth.Tools.Bootstrap.Compiler.Core.Promises;
◊using Azoth.Tools.Bootstrap.Compiler.Symbols;
◊using Azoth.Tools.Bootstrap.Compiler.Types;
◊using Azoth.Tools.Bootstrap.Compiler.Types.Declared;
◊using Azoth.Tools.Bootstrap.Framework;

'IAbstractSyntax' = Span:'TextSpan';

// ---------- Special Parts
BodyOrBlock = Statements:Statement*;
ElseClause;
BlockOrResult: ElseClause;

// ---------- Bindings
Binding = Symbol:'BindingSymbol';
LocalBinding: Binding = Symbol:'NamedBindingSymbol';

// ---------- Declarations
// Note: in the AST all declarations are entities because there are no namespace declarations
Declaration = File:'CodeFile' Symbol:'Symbol' NameSpan:'TextSpan';
/// A declaration that could contain executable code (i.e. invocable declaration or field declaration)
ExecutableDeclaration: Declaration;
InvocableDeclaration: Declaration = Symbol:'InvocableSymbol' Parameters:ConstructorOrInitializerParameter*;
ConcreteInvocableDeclaration: InvocableDeclaration, ExecutableDeclaration = Body;
ConcreteFunctionInvocableDeclaration: ConcreteInvocableDeclaration = Symbol:'FunctionSymbol' Parameters:NamedParameter* Body;

// ---------- Non-Member Declarations
NonMemberDeclaration: Declaration;
TypeDeclaration: NonMemberDeclaration = Symbol:'UserTypeSymbol' Supertypes:TypeDeclaration* Members:MemberDeclaration*;
ClassOrStructDeclaration: TypeDeclaration;
ClassDeclaration: ClassOrStructDeclaration = BaseClass:ClassDeclaration? Supertypes:TypeDeclaration* Members:ClassMemberDeclaration* DefaultConstructorSymbol:'ConstructorSymbol'?;
StructDeclaration: ClassOrStructDeclaration = Supertypes:TypeDeclaration* DefaultInitializerSymbol:'InitializerSymbol'? Members:StructMemberDeclaration*;
TraitDeclaration: TypeDeclaration = Supertypes:TypeDeclaration* Members:TraitMemberDeclaration*;
FunctionDeclaration: NonMemberDeclaration, ConcreteFunctionInvocableDeclaration = Attributes:Attribute* Symbol:'FunctionSymbol' Parameters:NamedParameter* Body;

// ---------- Member Declarations
// Note: by declaring the DeclaringType property type with quotes, it avoids the system assuming it must represent a child node
MemberDeclaration: Declaration = DeclaringType:'ITypeDeclaration';
ClassMemberDeclaration: MemberDeclaration;
StructMemberDeclaration: MemberDeclaration;
TraitMemberDeclaration: MemberDeclaration;
MethodDeclaration: ClassMemberDeclaration, TraitMemberDeclaration = Symbol:'MethodSymbol' SelfParameter Parameters:NamedParameter*;
AbstractMethodDeclaration: MethodDeclaration = SelfParameter Parameters:NamedParameter*;
ConcreteMethodDeclaration: MethodDeclaration, StructMemberDeclaration, ConcreteInvocableDeclaration = Symbol:'MethodSymbol' SelfParameter Parameters:NamedParameter* Body;
StandardMethodDeclaration: ConcreteMethodDeclaration = SelfParameter Parameters:NamedParameter* Body;
GetterMethodDeclaration: ConcreteMethodDeclaration= SelfParameter Parameters:NamedParameter* Body;
SetterMethodDeclaration: ConcreteMethodDeclaration = SelfParameter Parameters:NamedParameter* Body;
ConstructorDeclaration: ClassMemberDeclaration, ConcreteInvocableDeclaration = DeclaringType:'IClassDeclaration' Symbol:'ConstructorSymbol' SelfParameter Parameters:ConstructorOrInitializerParameter* Body;
InitializerDeclaration: StructMemberDeclaration, ConcreteInvocableDeclaration = DeclaringType:'IStructDeclaration' Symbol:'InitializerSymbol' SelfParameter Parameters:ConstructorOrInitializerParameter* Body;
FieldDeclaration: ClassMemberDeclaration, StructMemberDeclaration, ExecutableDeclaration, Binding = DeclaringType:'IClassOrStructDeclaration' Symbol:'FieldSymbol';
AssociatedFunctionDeclaration: ClassMemberDeclaration, StructMemberDeclaration, TraitMemberDeclaration, ConcreteFunctionInvocableDeclaration = Symbol:'FunctionSymbol' Parameters:NamedParameter* Body;

// ---------- Attributes
Attribute = ReferencedSymbol:'TypeSymbol';

// ---------- Parameters --------------
Parameter = Unused:'bool';
/// A parameter that can be declared in a constructor
ConstructorOrInitializerParameter: Parameter;
/// A parameter that creates a binding, i.e. a named or self parameter
BindingParameter: Parameter, Binding;
NamedParameter: Parameter, ConstructorOrInitializerParameter, BindingParameter, LocalBinding = Symbol:'VariableSymbol' DefaultValue:Expression?;
SelfParameter: Parameter, BindingParameter = Symbol:'SelfParameterSymbol';
FieldParameter: Parameter, ConstructorOrInitializerParameter = ReferencedSymbol:'FieldSymbol' DefaultValue:Expression?;

// ---------- Function Parts
Body: BodyOrBlock = Statements:BodyStatement*;

// ---------- Statements
Statement;
ResultStatement: Statement, BlockOrResult = Expression;
BodyStatement: Statement;
VariableDeclarationStatement: BodyStatement, LocalBinding = NameSpan:'TextSpan' Symbol:'VariableSymbol' Initializer:Expression? VariableIsLiveAfter:'Promise<bool>';
ExpressionStatement: BodyStatement = Expression;

// ---------- Patterns
Pattern;
BindingPattern: Pattern, LocalBinding = Symbol:'VariableSymbol' VariableIsLiveAfter:'Promise<bool>';
BindingContextPattern: Pattern = Pattern Type:'DataType';
OptionalPattern: Pattern = Pattern;

// ---------- Expressions
Expression = DataType:'DataType';
NameExpression: Expression;
AssignableExpression: Expression;
BlockExpression: Expression, BlockOrResult, BodyOrBlock = Statements:Statement*;
NewObjectExpression: Expression =  ReferencedSymbol:'ConstructorSymbol' Arguments:Expression*;
UnsafeExpression: Expression = Expression;

// ---------- Literal Expressions
LiteralExpression: Expression;
BoolLiteralExpression: LiteralExpression = Value:'bool';
IntegerLiteralExpression: LiteralExpression = Value:'BigInteger';
NoneLiteralExpression: LiteralExpression;
StringLiteralExpression: LiteralExpression = Value:'string';

// ---------- Operator Expressions
AssignmentExpression: Expression = LeftOperand:AssignableExpression Operator:'AssignmentOperator' RightOperand:Expression;
BinaryOperatorExpression: Expression = LeftOperand:Expression Operator:'BinaryOperator' RightOperand:Expression;
UnaryOperatorExpression: Expression = Fixity:'UnaryOperatorFixity' Operator:'UnaryOperator' Operand:Expression;
PatternMatchExpression: Expression = Referent:Expression Pattern;

// ---------- Control Flow Expressions
IfExpression: Expression, ElseClause = Condition:Expression ThenBlock:BlockOrResult ElseClause?;
LoopExpression: Expression = Block:BlockExpression;
WhileExpression: Expression = Condition:Expression Block:BlockExpression;
ForeachExpression: Expression, LocalBinding = Symbol:'VariableSymbol' InExpression:Expression IterateMethod:'MethodSymbol?' NextMethod:'MethodSymbol' Block:BlockExpression VariableIsLiveAfterAssignment:'Promise<bool>';
BreakExpression: Expression = Value:Expression?;
NextExpression: Expression;
ReturnExpression: Expression = Value:Expression?;

// ---------- Implicit Conversion Expressions
ImplicitConversionExpression: Expression = Expression DataType:'DataType';
//ImplicitImmutabilityConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'ObjectType';
ImplicitSimpleTypeConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'SimpleType';
ImplicitOptionalConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'OptionalType';
ImplicitLiftedConversionExpression: ImplicitConversionExpression = Expression ConvertToType:'OptionalType';

// ----------- Explicit Conversion Expressions
ExplicitConversionExpression: Expression = Expression IsOptional:'bool' DataType:'DataType';
ExplicitSimpleTypeConversionExpression: ExplicitConversionExpression = Expression ConvertToType:'SimpleType';

// ---------- Invocation Expressions
InvocationExpression: Expression = Arguments:Expression*;
FunctionInvocationExpression: InvocationExpression = ReferencedSymbol:'FunctionSymbol' Arguments:Expression*;
InitializerInvocationExpression: InvocationExpression = ReferencedSymbol:'InitializerSymbol' Arguments:Expression*;
MethodInvocationExpression: InvocationExpression = Context:Expression ReferencedSymbol:'MethodSymbol' Arguments:Expression*;
FunctionReferenceInvocationExpression: InvocationExpression = Referent:Expression Arguments:Expression*;

// ---------- Variable Expressions
VariableNameExpression: AssignableExpression, NameExpression = ReferencedSymbol:'VariableSymbol' IsMove:'bool' VariableIsLiveAfter:'Promise<bool>';
SelfExpression: Expression = ReferencedSymbol:'SelfParameterSymbol' IsImplicit:'bool';
FieldAccessExpression: AssignableExpression = Context:Expression AccessOperator:'AccessOperator' ReferencedSymbol:'FieldSymbol';

// ---------- Capability Expressions
MoveExpression: Expression = ReferencedSymbol:'BindingSymbol' Referent:Expression;
TempMoveExpression: Expression = Referent:Expression;
FreezeExpression: Expression = ReferencedSymbol:'BindingSymbol' Referent:Expression;
TempFreezeExpression: Expression = Referent:Expression;
IdExpression: Expression = Referent:Expression;

// ---------- Recover Expressions
RecoverExpression: Expression = Value:Expression;
RecoverConstExpression: RecoverExpression = Value:Expression;
RecoverIsolationExpression: RecoverExpression = Value:Expression;

// ---------- Async Expressions
AsyncBlockExpression: Expression = Block:BlockExpression;
AsyncStartExpression: Expression = Scheduled:'bool' Expression;
AwaitExpression: Expression = Expression;

// ---------- Name Expressions
FunctionNameExpression: NameExpression = ReferencedSymbol:'FunctionSymbol';
